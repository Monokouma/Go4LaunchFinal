// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.4.0' apply false
    id 'com.android.library' version '7.4.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.20' apply false
    id 'com.google.dagger.hilt.android' version '2.44.2' apply false
    id "org.jetbrains.kotlinx.kover" version "0.6.1"
    id 'com.google.gms.google-services' version '4.3.10' apply false
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin' version '2.0.1'
}

subprojects {
    tasks.withType(Test) {
        testLogging {
            events "standardOut", "skipped", "failed"

            exceptionFormat = "full"
            showExceptions = true
            showCauses = true
            showStackTraces = true
        }
    }
}

koverMerged {
    enable()

    filters {
        classes {
            excludes.addAll(
                    [
                            // Hilt
                            "dagger.hilt.*",
                            "hilt_aggregated_deps.*",
                            "*_Factory",
                            "*_Factory\$*",
                            "*_*Factory",
                            "*_*Factory\$*",
                            "*_Impl",
                            "*_Impl\$*",
                            "*.DataModule",
                            "*.DataModule\$*",
                            "*_MembersInjector",
                            "*_HiltModules",
                            "*_HiltModules\$*",

                            // Gradle Generated
                            "com.despaircorp.myapplication.BuildConfig",
                            "com.despaircorp.myapplication.data.BuildConfigResolver", // Can't mockK static field: https://github.com/mockk/mockk/issues/107
                            "com.despaircorp.domain.BuildConfig",
                            "com.despaircorp.ui.BuildConfig",
                            "com.despaircorp.data.BuildConfig",
                            // TODO: Remove below when Kover can handle Android integration tests!
                            // Android UI
                            "*MainApplication",
                            "*MainApplication\$*",
                            "*Fragment",
                            "*Fragment\$*",
                            "*Activity",
                            "*Activity\$*",
                            "*AppDatabase",
                            "*AppDatabase\$*",
                            "com.despaircorp.myapplication.databinding.*", // ViewBinding
                            "*Adapter",
                            "*Adapter\$*",
                    ]
            )
        }
    }
}